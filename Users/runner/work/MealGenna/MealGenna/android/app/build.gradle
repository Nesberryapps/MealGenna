import groovy.json.JsonSlurper

def appConfig() {
    def configFile = new File(project.projectDir, "../../capacitor.config.json")
    if (!configFile.exists()) {
        return [:]
    }
    return new JsonSlurper().parseText(configFile.text)
}

def config = appConfig()
def appId = config.appId ?: "com.example.app"
def appName = config.appName ?: "MyApp"
def appVersion = System.getenv("CAPACITOR_ANDROID_VERSION_NAME") ?: "1.0"
def appVersionCode = Integer.parseInt(System.getenv("CAPACITOR_ANDROID_VERSION_CODE") ?: "1")

android {
    namespace appId
    compileSdkVersion 34

    defaultConfig {
        applicationId appId
        minSdkVersion 22
        targetSdkVersion 34
        versionCode appVersionCode
        versionName appVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('keystore.jks')
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("ANDROID_ALIAS")
            keyPassword System.getenv("ANDROID_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        dataBinding true
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation project(':capacitor-android')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation project(':capacitor-community-admob')
    implementation project(':capacitor-filesystem')
    implementation project(':capacitor-local-notifications')
}
